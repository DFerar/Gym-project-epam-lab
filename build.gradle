plugins {
    id 'java'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '10.8.1'
    id 'checkstyle'
    id 'war'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

ext {
    springCloudVersion = "2023.0.1"
}

group = 'com.gym'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava {
    options.compilerArgs << '-parameters'
}

dependencies {
    //core
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    //database
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.6.0'
    //security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    //communication
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.apache.kafka:kafka-clients:3.1.0'
    //utils
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.2.0'
    implementation 'io.micrometer:micrometer-registry-prometheus'
}

test {
    useJUnitPlatform()
    include("**/*Test.class")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/gym/GymCRMApp.*",
                    "com/gym/entity/**.*",
                    "com/gym/repositories/**.*",
                    "com/gym/requestDto/customerRequest/**.*",
                    "com/gym/requestDto/instructorRequest/**.*",
                    "com/gym/requestDto/loginRequest/**.*",
                    "com/gym/requestDto/trainingRequest/**.*",
                    "com/gym/responseDto/customerResponse/**.*",
                    "com/gym/responseDto/instructorResponse/**.*",
                    "com/gym/responseDto/trainingResponse/**.*",
                    "com/gym/configuration/**.*",
                    "com/gym/logging/**.*",
                    "com/gym/AppConfig.*"
            ])
        }))
    }
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
